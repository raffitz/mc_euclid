%name: cube
%author: raffitz
%description: A cube stood on its vertex

# Cube bounding box size:

# Figure 1:
#       _*_         -
#     _/ | \_       ^
#   _/   |   \_     |
#  *     |     *    h
#  |     |     |    e
#  |    _*_    |    i
#  |  _/   \_  |    g
#  |_/       \_|    h
#  *_         _*    t
#    \_     _/      |
#      \_ _/        v
#        *          -
#
#  |<--width-->| 

# From this diagram, you can tell that the width is the diagonal of the square.
# We know as a corollary of Pythagoras's theorem that the diagonal of a square
# of side s is sqrt(2)*s.

width: sqrt 2 * s

# Figure 2:
#          _*__         -
#         /    \__      ^
#       _/        \__   d
#     _*__          _*  e
#   _/    \__      /    p
#  *__       \__ _/     t
#     \__      _*       h
#        \__ _/         v
#           *           -
#
#  |<----height----->|

# From this diagram, you can tell that the depth is the diagonal of the cube.
# From Pythagoras's theorem we know that the diagonal of a cube of side s is
# sqrt((sqrt(2)^2)s^2 + s^2) = sqrt(3)*s.

depth: sqrt 3 * s

# If we consider the x = 0 section of the image above, we get the following:
# Figure 3:
#          _*__         -
#         /    \__      ^
#       _/  |     \__   d
#     _/    b       _*  e
#   _/      |      /    p
#  *__ -a- -*    _/     t
#     \__   |  _/       h
#        \__c_/         v
#           *           -
#
#  |<----height----->|

# We know that:
# a^2 + b^2 = (sqrt(2)*s)^2
# a^2 + c^2 = s
# b + c = sqrt(3)*s

# From that, we get that a = (2/3)^(1/2)*s, b = (2*s)/(3^(1/2)), c = s/(3^(1/2))
# And we know that height = 2 * a

height: 2 * sqrt(2/3) * s

# Cube bounding box position:

# Considering that the cube's center is the origin, we just need to negate and
# half the bounding box sizes to get the minimum boundaries for each variable.
min_x: 0 - (s * sqrt 2) / 2
min_y: 0 - (s * sqrt (2/3))
min_z: 0 - (s * sqrt 3) / 2

# The cube is the intersection of 6 semi-spaces.
# Each semi-space is defined by an inequality based on the plane that defines
# its boundary.
# The plane equations are achieved through a point belonging to the plane, as
# well as a vector normal to the plane.

# The easiest plane to construct is the one that is invariant in the x
# dimension, that is, parallel to the x axis.

# For ease of calculation, the point used is the point (0,0,(s * 3^(1/2))/2).
# The vector used will have the coordinates (0,-a,c) 

# This means the plane equation starts out as:
# -a*y + c*(z-((s * 3^(1/2))/2)) <= 0
# Which can be simplified to:
# -a*y + c*z <= (s^2)/2
# Given that a and c are both expressed as functions of s, we can factor out
# the s and get the following:
# y * (0 - (2/3) ^ (1/2)) + ((1/3) ^ (1/2)) * z <= s / 2

y * (0 - sqrt (2/3)) + sqrt(1/3) * z <= s / 2

# By using the same point and rotating the vector by +-120 degrees or +-2*pi/3
# radians, we get the following two plane inequalities.

x * (0 - sqrt 2/2) + y * sqrt(1/6) + sqrt(1/3) * z <= s / 2
x * sqrt 2/2 + y * sqrt(1/6) + sqrt(1/3) * z <= s / 2

# By replacing the point with (0,0,0-(s * 3^(1/2))/2) and scaling each of the
# three vectors by (-1), we get the remaining three plane inequalities.

y * sqrt(2/3) - sqrt(1/3) * z <= s / 2
x * (0 - sqrt 2/2) - y * sqrt(1/6) - sqrt(1/3) * z <= s / 2
x * sqrt 2/2 - y * sqrt(1/6) - sqrt(1/3) * z <= s / 2

